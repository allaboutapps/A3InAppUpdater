plugins {
    id "com.jfrog.bintray" version "1.8.1"
    id 'digital.wup.android-maven-publish' version '3.6.2'
}


group = 'at.allaboutapps.inappupdater'
version = '1.2.1'

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka-android'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        // the name of the artifact
        archivesBaseName = 'a3inappupdater' // e.g. a3webview

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

bintray {
    user = 'allaboutapps'
    // read pw from secure travis.yml
    key = System.getenv('BINTRAY_KEY')

    pkg {
        repo = 'A3-Android'
        name = "$project.group.$archivesBaseName"

        println name
        version {
            name = project.version
            desc = ''
            released = new Date()
            vcsTag = System.getenv('TRAVIS_TAG')
        }
        dryRun = false
        publish = true
        licenses = ['MIT']
        vcsUrl = 'https://github.com/allaboutapps/InAppUpdater'
        websiteUrl = 'http://www.allaboutapps.at'
    }
    publications = ['mavenAar']
}

publishing {
    publications {
        mavenAar(MavenPublication) {
            from components.android
            pom {
                name = 'InAppUpdater'
                description = 'Android library to simplify In App Update process'
                url = 'https://github.com/allaboutapps/InAppUpdater'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'MAG'
                        name = 'Markus Gursch'
                        email = 'markus.gursch@allaboutapps.at'
                    }
                }
            }
        }
    }
}

task javadoc(type: Javadoc) {
    excludes = ['**/*.kt'] // Exclude all kotlin files from javadoc file.
}

dokka {
    outputFormat = 'html'
    outputDirectory = javadoc.destinationDir
}

task generateJavadocsJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from dokka.outputDirectory
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath()
            .join(File.pathSeparator))
}

artifacts {
    archives generateSourcesJar, generateJavadocsJar
}

android {
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'com.google.android.play:core:1.6.3'
    implementation 'androidx.annotation:annotation:1.2.0'

    implementation 'io.reactivex.rxjava3:rxjava:3.0.11'

    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

apply plugin: 'digital.wup.android-maven-publish'
